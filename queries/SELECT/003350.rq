PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX cube: <https://cube.link/>
SELECT ?time (SUM(COALESCE(?deathCount, 0 )) AS ?totalDeathCount) (SUM(COALESCE(?populationCount, 0 )) AS ?totalPopulationCount) (((SUM(COALESCE(?deathCount, 0 ))) / (SUM(COALESCE(?populationCount, 0 )))) * 100  AS ?deathPercentage) WHERE {
  {
    <https://ld.stadt-zuerich.ch/statistics/000369/observation/> cube:observation ?observation1.
    ?observation1 <https://ld.stadt-zuerich.ch/statistics/property/RAUM> <https://ld.stadt-zuerich.ch/statistics/code/R30000>;
      <https://ld.stadt-zuerich.ch/statistics/measure/GES> ?deathCount;
      <https://ld.stadt-zuerich.ch/statistics/property/TIME> ?time.
    FILTER(?deathCount != "NaN"^^xsd:double)
  }
  UNION
  {
    <https://ld.stadt-zuerich.ch/statistics/000201/observation/> cube:observation ?observation2.
    ?observation2 <https://ld.stadt-zuerich.ch/statistics/property/RAUM> <https://ld.stadt-zuerich.ch/statistics/code/R30000>;
      <https://ld.stadt-zuerich.ch/statistics/measure/BEW> ?populationCount;
      <https://ld.stadt-zuerich.ch/statistics/property/TIME> ?time.
    FILTER(?populationCount != "NaN"^^xsd:double)
  }
}
GROUP BY ?time
ORDER BY (?time)